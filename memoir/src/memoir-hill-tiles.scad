/* Memoir '44 Tile Set: Hills
 *
 * This file defines modules to build three types of hills:
 * - simpleHillTile - simple, one-tile hill
 * - connectedHillTile - one side is designed to abut another 
 *                       connectedHillTile or doubleConnectedHillTile
 * - doubleConnectedHillTile - two sides designed to abut others
 *
 * Obviously one could simply use the freestanding version.
 *
 * All three follow the same approach, which is basically to use a hull() of
 * a blank tile in conjunction with an extruded polygon (the fooPolyhedron()
 * call) to create a base, followed by the same polygon extruded with an
 * angled side to create the sides of the hill and hilltop.
 *
 * In case you are interested, the polyhedron code was generated by drawing
 * the polygon in Inkscape and then using a perl script that parses the
 * output of the Inkscape SCAD output routines. These polyhedrons are kept
 * in separate SCAD files in case I need to regenerate them.
 *
 * Dimensions of a tile (as a reminder):
 * - point to point, tile is 62mm
 *   - center to center of edge is 26.847mm
 * - stock, cardboard tiles in Memoir are approx. 1.5mm tall
 */

include <hill-polyhedron.scad>
include <connhill-polyhedron.scad>
include <doubleconnhill-polyhedron.scad>

simpleHillTile();

module hillBlankTile(height=1.5) {
    cylinder(r = 62/2, h = height, $fn = 6, center = [0,0]);
}

module simpleHillTile(height=6.5) {
    hull() {
	hillBlankTile();
	hillPolyhedron(height=2.5,angle=0);
    }

    translate([0,0,2.5])  
	hillPolyhedron(height=height-2.5,angle=30);
}

module connectedHillTile(height=6.5) {
    hull() {
	hillBlankTile();
	translate([-0.8395,-0.785,0])
	    connHillPolyhedron(height=2.5,angle=0);
    }

    translate([-0.8395,-0.785,2.5])
	connHillPolyhedron(height=height-2.5,angle=30);
}

module doubleConnectedHillTile(height=6.5) {
    hull() {
	hillBlankTile();
	translate([-2.9015,-0.7865,0])
	    doubleConnHillPolyhedron(height=2.5);
    }

    translate([-2.9015,-0.7865,2.5])
	doubleConnHillPolyhedron(height=height-2.5,angle=30);
}

/*
 * Local variables:
 *  mode: C
 *  c-indent-level: 4
 *  c-basic-offset: 4
 * End:
 */
